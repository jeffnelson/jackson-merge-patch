buildscript {
	repositories { mavenCentral() }
}

repositories {
	mavenCentral()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'jacoco'

group = 'com.github.jeffnelson'
version = currentVersion
archivesBaseName = project.name

sourceCompatibility = 1.8

dependencies {
	compile 'com.fasterxml.jackson.core:jackson-databind:2.8.11'
	
	compileOnly 'org.projectlombok:lombok:1.16.22'
	
	testCompileOnly 'org.projectlombok:lombok:1.16.22'
	testCompile 'org.springframework.boot:spring-boot-starter-test:1.5.4.RELEASE'
	testCompile 'org.springframework.boot:spring-boot-starter-web:1.5.4.RELEASE'
    testCompile 'com.google.guava:guava:28.0-jre'
    testCompile 'commons-io:commons-io:2.6'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            includes = [ 'com.github.jeffnelson.jackson.patch.deser.PatchFieldDeserializer' ]
            excludes = [ ]

            limit { minimum = 1.0 }
        }
    }
}

// always run jacocoTestReport and coverageVerification
test.finalizedBy(jacocoTestReport)
jacocoTestReport.finalizedBy(jacocoTestCoverageVerification)

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}


